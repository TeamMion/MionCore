buildscript {
    repositories {
        jcenter()
        
        maven {
            url "http://files.minecraftforge.net/maven"
        }
        
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

version = modversion
group = "teammion.$modid"
archivesBaseName = "${modid}_${mcversion}"

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "$mcversion-$forgeversion"
    
    runDir = "run"
    
    mappings = mcpmappings
    
    makeObfSourceJar = false
}

dependencies {
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output
}

task apiJar(type : Jar) {
    from sourceSets.api.output
    
    classifier = "api"
}

task deobfJar(type : Jar) {
    from sourceSets.api.output
    from sourceSets.main.output
    
    classifier = "deobf"
}

task deobfSourceJar(type : Jar) {
    from sourceSets.api.java
    from sourceSets.main.java
    from sourceSets.main.resources
    
    classifier = "sources"
}

artifacts {
    archives apiJar
    archives deobfJar
    archives deobfSourceJar
}

javadoc {
    source = sourceSets.api.java
    
    options.author = true
}

curseforge {
    apiKey = System.getenv().CURSE_API_KEY ? System.getenv().CURSE_API_KEY : "devBuild"
    
    project {
        id = curse_id
        
        changelog = ""
        
        releaseType = type == "rc" ? "release" : type
        
        addGameVersion mcversion
        
        mainArtifact jar
        
        addArtifact deobfSourceJar
        addArtifact apiJar
        addArtifact deobfJar
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
